const std = @import("std");

const pabrikcore = @import("pabrikcore");

// Puts pabrik's crash reporter at the FRONT of the Windows exception chain
// (std's vectored handler would otherwise swallow access violations).
// See `crash_handler.root_debug` for the full ordering argument.
pub const debug = pabrikcore.crash_handler.root_debug;

const ai_mod = pabrikcore.ai_mod;
const helpers = @import("helpers");
const gserverz = pabrikcore.gserverz;
const http_routes = pabrikcore.http_routes;
const static_files = pabrikcore.static_files;
// Re-exported via pabrikcore so the exe module never @imports the file
// directly (dual-module error — see root.zig).
const cleanup_stale_worker = pabrikcore.cleanup_stale_worker;
const cleanup_stale_background_process = pabrikcore.cleanup_stale_background_process;

// Boot phases (extracted from this file): the exe module reaches them via
// pabrikcore.* for the same dual-module reason as above.
const shutdown = pabrikcore.boot_shutdown;
const cli_dispatch = pabrikcore.boot_cli_dispatch;
const server_boot = pabrikcore.boot_server_boot;
const static_serve = pabrikcore.http_static_serve;

pub fn main(init: std.process.Init) !void {
    const allocator = init.gpa;
    const environment = init.environ_map;
    const io = init.io;

    if (try cli_dispatch.dispatchServiceSubcommand(allocator, io, environment, init)) return;
    if (try cli_dispatch.dispatchCreateAdmin(allocator, io, environment, init)) return;

    server_boot.ignoreSigpipe();

    // Flatten argv for the parser (it dupes what it keeps: the iterator's
    // buffers are freed on Windows).
    var args_iter = try std.process.Args.Iterator.initAllocator(init.minimal.args, allocator);
    defer args_iter.deinit();
    var argv_flat: std.ArrayList([]const u8) = .empty;
    defer argv_flat.deinit(allocator);
    while (args_iter.next()) |arg| try argv_flat.append(allocator, arg);

    // `var` because --tls-selfsigned overwrites the parsed cert/key paths.
    var cli = (try server_boot.parseCliArgs(allocator, argv_flat.items)) orelse return;
    const tls_ctx = try server_boot.setupTlsCtx(allocator, environment, &cli);

    if (init.environ_map.get("HOME")) |home| {
        std.log.info("HOME={s}", .{home});
    }

    var llm_config = pabrikcore.config.LlmConfig.init(allocator, io, null, environment) catch |err| {
        std.log.err("Failed to load config: {s}", .{@errorName(err)});
        return err;
    };
    // Heap-moved for the holder swap below; freed via `freeAllLlmConfigs`
    // at shutdown, so no defer here. Empty configs only warn — the server
    // still boots for non-LLM endpoints (the PUT handler keeps rejecting
    // empty api_keys on edit).
    if (llm_config.validate()) |_| {} else |err| {
        std.log.warn(
            "Config validation: {s}. LLM calls will fail until api_key/model/base_url are populated in ~/.config/pabrik/config.json.",
            .{@errorName(err)},
        );
    }

    const initial_llm_config_ptr = try allocator.create(pabrikcore.config.LlmConfig);
    errdefer allocator.destroy(initial_llm_config_ptr);
    initial_llm_config_ptr.* = llm_config;

    var db_handles = try server_boot.openDatabase(allocator, io, environment);
    defer db_handles.deinit(allocator);
    const dbSqlite = &db_handles.db;

    const tmp_path = environment.get("TMPDIR") orelse
        environment.get("TEMP") orelse
        environment.get("TMP") orelse
        "/tmp";
    const log_file_path = try std.fs.path.join(allocator, &.{ tmp_path, "agentic_coding.log" });
    defer allocator.free(log_file_path);

    pabrikcore.setPanicLogPath(log_file_path);
    // Crash handlers go in BEFORE the HTTP server starts, sharing the panic
    // log path (see src/service/crash_handler.zig for the contract).
    pabrikcore.crash_handler.setCrashLogPath(log_file_path);
    pabrikcore.crash_handler.installCrashHandlers();

    pabrikcore.loggermod.initGlobalColor(allocator, io, .{
        .min_level = .debug,
        .output_mode = .file,
        .log_file_path = log_file_path,
        .include_location = true,
        .include_request_id = true,
        .include_timestamp = true,
    });
    defer pabrikcore.loggermod.deinitGlobal(io);

    const global_logger_ptr = pabrikcore.loggermod.getGlobal().?;

    const ctxParent = try allocator.create(pabrikcore.App);
    defer allocator.destroy(ctxParent);
    ctxParent.* = pabrikcore.App{
        .allocator = allocator,
        .io = io,
        .db = dbSqlite,
        .llm_config_holder = .{ .current = initial_llm_config_ptr },
        .logger = global_logger_ptr,
        .environment = environment,
        .active_loops = undefined, // set below after initialization
        .event_bus = undefined, // set below after initialization
        .server = undefined, // set below after initialization
        .group_emit_session_create = .init,
        .group_bg_watchers = .init,
    };

    // `--no-static-dir` wins over `--static-dir` regardless of order.
    if (cli.no_static_dir) {
        if (cli.static_dir) |dir_arg| allocator.free(dir_arg);
        std.log.info("--no-static-dir: API only, no static files at /.", .{});
    } else if (cli.static_dir) |dir_arg| ctxParent.static_dir_path = dir_arg;
    ctxParent.auth_enabled = cli.auth_enabled;
    if (cli.auth_enabled) {
        std.log.info("--auth on: per-user LLM config comes from users.config_json; config.json is ignored.", .{});
    }

    _ = try pabrikcore.setSingleton(ctxParent);

    // Eager globals: call sites go through the singleton instead of
    // lazy-init on first MCP use; the defers free the arenas at shutdown.
    ctxParent.mcp_stdio_registry = pabrikcore.mcp_stdio.StdioRegistry.global(allocator);
    ctxParent.mcp_http_registry = pabrikcore.mcp_http.HttpRegistry.global(allocator);
    defer pabrikcore.mcp_stdio.StdioRegistry.deinitGlobal();
    defer pabrikcore.mcp_http.HttpRegistry.deinitGlobal();

    const event_bus_mod = pabrikcore.event_bus;
    var event_bus = event_bus_mod.EventBus.init("my-bus", allocator, io);
    defer event_bus.deinit();
    ctxParent.event_bus = &event_bus;

    // Routine scheduler: a concurrent Io task (no thread spawned), after
    // setSingleton + event bus so its fire path can emit events.
    ai_mod.startup.start(allocator, dbSqlite, ctxParent, io) catch |err| {
        std.log.err("Failed to submit routine scheduler: {s}", .{@errorName(err)});
    };

    var active_loops = ai_mod.models.ActiveLoops.init(allocator);
    defer active_loops.deinit(allocator);
    ctxParent.active_loops = &active_loops;

    // Explicit --port wins; otherwise web_launch_enabled decides.
    var port: u16 = server_boot.resolvePort(cli.port, llm_config.web_launch_enabled);
    if (port == 0) {
        port = pabrikcore.web_port.pickFreePort(io) catch |err| {
            std.log.err("web launch: no free port in [{d},{d}]: {s}", .{
                pabrikcore.web_port.web_port_range_start,
                pabrikcore.web_port.web_port_range_end,
                @errorName(err),
            });
            return err;
        };
        std.log.info("web launch: browser mode on http://127.0.0.1:{d}/", .{port});
    }

    const address = gserverz.Address.init("127.0.0.1", port) catch |err| switch (err) {
        // A busy port is an operator error, not a crash: report + exit(1)
        // instead of dying with SIGSEGV down the runtime error path.
        error.BindFailed => {
            std.log.err("cannot bind 127.0.0.1:{d} - address already in use (is another pabrik already running on this port?)", .{port});
            std.process.exit(1);
        },
        else => return err,
    };
    const gs = try gserverz.GinwaServer.init(allocator, io, address);
    defer gs.deinit();
    gs.enable_h2c = cli.enable_h2c;

    // SIGINT/SIGTERM close the listener via shutdown(), unblocking listen()
    // so the post-listen block below runs. Second signal force-exits (130).
    shutdown.setServer(gs);
    shutdown.reset();
    pabrikcore.signal_handlers.installShutdownHandlers(shutdown.handleShutdownSignal);

    if (tls_ctx) |ctx| gs.setTlsCtx(ctx);
    if (cli.enable_h2c) std.debug.print("HTTP/2 (h2c) enabled on this port (HTTP/1.1 clients unaffected)\n", .{});

    var static_dir_cfg: ?*static_files.StaticDirConfig = null;
    defer if (static_dir_cfg) |cfg| server_boot.freeStaticDir(allocator, cfg);

    if (ctxParent.static_dir_path) |dir| {
        const cfg = try server_boot.setupStaticDir(allocator, io, dir);
        static_dir_cfg = cfg;
        gs.setStaticDirHandler(static_serve.staticDirHandler, @ptrCast(cfg));
    }

    var group: std.Io.Group = .init;
    defer group.cancel(io);

    try group.concurrent(
        io,
        struct {
            fn run(gss: *gserverz.GinwaServer) void {
                gss.sse_manager.startEventLoop(5) catch {};
            }
        }.run,
        .{gs},
    );

    ctxParent.server = gs;
    try http_routes.registerAll(gs);

    _ = try event_bus.subscribe(ai_mod.ai_workflow.RunParamsNew, "ai_worker_flow", ai_mod.ai_workflow.CallbackAiWorkerFlow.callback);
    ctxParent.server.sse_manager.on_disconnect = ai_mod.handleClientDisconnect;

    std.debug.print("Agent is ready to serve!\n", .{});

    const boot_unix = std.Io.Clock.now(.real, io).toSeconds();
    _ = gs.cronjob_manager.register(
        "* * * * *",
        "cleanup_stale_worker",
        cleanup_stale_worker.handle,
        null,
        boot_unix,
    ) catch |err| {
        std.debug.print("Failed to register heartbeat cron: {s}\n", .{@errorName(err)});
    };
    _ = gs.cronjob_manager.register(
        "* * * * *",
        "cleanup_stale_background_process",
        cleanup_stale_background_process.handle,
        null,
        boot_unix,
    ) catch |err| {
        std.debug.print("Failed to register cleanup_stale_background_process cron: {s}\n", .{@errorName(err)});
    };

    try gs.listenEventLoop(.{ .dispatch_mode = .worker_pool });

    // Post-listen shutdown. The signal path only sets the flag (handlers
    // can't log), so logging happens here.
    if (shutdown.isRequested()) {
        std.log.info("shutdown signal received — listener closed, flushing in-flight requests", .{});
        // Exit instead of unwinding: full unwind segfaults (defer teardowns
        // race draining Io worker threads) and the stops race
        // signal-interrupted workers. Nothing is freed afterwards so no
        // thread outlives its context; exit(0) reaps everything. WAL mode
        // replays uncheckpointed frames on next boot. A second signal
        // force-exits (130) straight from the handler.
        helpers.sleepMillis(50);
        std.process.exit(0);
    }

    // Only /test/shutdown reaches here (the signal path exits above): join
    // the cron thread BEFORE the defers free its context, or it segfaults
    // ~10s later (rc=-11).
    gs.cronjob_manager.stop();
    gs.sse_manager.stop();
}
